plugins {
    id "com.github.johnrengelman.shadow" version "7.0.0"
    id "me.shedaniel.unified-publishing"
    id "maven-publish"
}

loom {
    accessWidenerPath = file("src/main/resources/cloth-config.accessWidener")
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

repositories {
    maven {
        url "https://maven.terraformersmc.com/releases/"
        content {
            includeGroup "com.terraformersmc"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${rootProject.architectury.minecraft}"
    mappings loom.officialMojangMappings()

    modApi "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"

    modApi("me.gamerben.hhapi:basic-math:0.6.1")
    include("me.gamerben.hhapi:basic-math:0.6.1")

    implementation("blue.endless:jankson:${rootProject.jankson_version}")
    implementation("com.moandjiezana.toml:toml4j:${rootProject.toml4j_version}")
    implementation("org.yaml:snakeyaml:${rootProject.snakeyaml_version}")
    shadowCommon("blue.endless:jankson:${rootProject.jankson_version}")
    shadowCommon("com.moandjiezana.toml:toml4j:${rootProject.toml4j_version}") {
        exclude(module: "gson")
    }
    shadowCommon("org.yaml:snakeyaml:${rootProject.snakeyaml_version}")

    modCompileOnly("com.terraformersmc:modmenu:${mod_menu_version}") {
        transitive(false)
    }
    modLocalRuntime("com.terraformersmc:modmenu:${mod_menu_version}") {
        transitive(false)
    }

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
}

processResources {
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
    inputs.property "version", project.version
}

shadowJar {
    relocate "blue.endless.jankson", "${rootProject.maven_group}.HalfHeartAPI.shadowed.blue.endless.jankson"
    relocate "com.moandjiezana.toml", "${rootProject.maven_group}.HalfHeartAPI.shadowed.com.moandjiezana.toml"
    relocate "org.yaml.snakeyaml", "${rootProject.maven_group}.HalfHeartAPI.shadowed.org.yaml.snakeyaml"
    
    configurations = [project.configurations.shadowCommon]
    classifier "shadow"
}

remapJar {
    input.set(shadowJar.archiveFile)
    dependsOn(shadowJar)
    archiveClassifier = "fabric"
    
    from rootProject.file("LICENSE.md")
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-fabric"
            artifact(remapJar.archivePath) {
                builtBy build
                classifier null
            }
        }
    }

    repositories {
        if (System.getenv("MAVEN_PASS") != null) {
            maven {
                url = "https://github.com/gamerbenyt/halfheartAPI/"
                credentials {
                    username = "gamerbenyt"
                    password = System.getenv("MAVEN_PASS")
                }
            }
        }
    }
}

unifiedPublishing {
    project {
        displayName = "[Fabric $rootProject.supported_version] v$project.version"
        releaseType = "release"
        gameVersions = ["1.19", "1.19.1", "1.19.2"]
        gameLoaders = ["fabric"]
        
        mainPublication remapJar
        
        if (project.hasProperty("curse_api_key") || System.getenv("curse_api_key") != null) {
            curseforge {
                token = project.hasProperty("curse_api_key") ? project.property("curse_api_key") : System.getenv("curse_api_key")
                id = project.curseforge_id
                gameVersions.addAll "1.19-Snapshot", "Java 17"
            }
        }

        if (project.hasProperty("modrinth_key") || System.getenv("modrinth_key") != null) {
            modrinth {
                token = project.hasProperty("modrinth_key") ? project.property("modrinth_key") : System.getenv("modrinth_key")
                id = project.modrinth_id
                version = "$project.version+$project.name"
            }
        }
    }
}

rootProject.tasks.getByName("modPublish").dependsOn tasks.getByName("publishUnified")
